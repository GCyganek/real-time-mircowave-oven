package microwave
public
	with
	Alarm, DoorSensor, Light, TemperatureSensor, TouchScreen, Turntable, Fan, Display, GrillHeater, Magnetron,
	MicrowaveProcessor, GrillHeaterProcessor, MagnetronProcessor;
	
	system MicrowaveSystem
	end MicrowaveSystem;
	
	system implementation MicrowaveSystem.impl
		subcomponents
			-- devices
			Alarm: device Alarm::Alarm;
			DoorSensor: device DoorSensor::DoorSensor;
			Light: device Light::Light;
			TemperatureSensor: device TemperatureSensor::TemperatureSensor;
			TouchScreen: device TouchScreen::TouchScreen;
			Turntable: device Turntable::Turntable;
			Fan: device Fan::Fan;
			Display: device Display::Display;
			GrillHeater: device GrillHeater::GrillHeater;
			Magnetron: device Magnetron::Magnetron;
			-- processor
			MicrowaveProcessor: processor MicrowaveProcessor::MicrowaveProcessor;
			MagnetronProcessor: processor MagnetronProcessor::MagnetronProcessor;
			GrillHeaterProcessor: processor GrillHeaterProcessor::GrillHeaterProcessor;
			-- processes
			MicrowaveProcessorProcess: process MicrowaveProcessor::MicrowaveProcessorProcess.impl;
			TouchScreenProcess: process MicrowaveProcessor::TouchScreenProcess.impl;
			MagnetronProcessorProcess: process MagnetronProcessor::MagnetronProcessorProcess.impl;
			GrillHeaterProcessorProcess: process GrillHeaterProcessor::GrillHeaterProcessorProcess.impl;
			-- buses
			MicrowaveBus: bus MicrowaveProcessor::MicrowaveBus;
			MagnetronBus: bus MagnetronProcessor::MagnetronBus;
			GrillHeaterBus: bus GrillHeaterProcessor::GrillHeaterBus;
		connections
			-- buses
			MicrowaveBusCon1: bus access MicrowaveBus -> MicrowaveProcessor.MicrowaveBus;
			MicrowaveBusCon2: bus access MicrowaveBus -> TouchScreen.MicrowaveBus;
			MicrowaveBusCon3: bus access MicrowaveBus -> TemperatureSensor.MicrowaveBus;
			MicrowaveBusCon4: bus access MicrowaveBus -> DoorSensor.MicrowaveBus;
			MicrowaveBusCon5: bus access MicrowaveBus -> Turntable.MicrowaveBus;
			MicrowaveBusCon6: bus access MicrowaveBus -> Display.MicrowaveBus;
			MicrowaveBusCon7: bus access MicrowaveBus -> Light.MicrowaveBus;
			MicrowaveBusCon8: bus access MicrowaveBus -> Alarm.MicrowaveBus;
			MicrowaveBusCon9: bus access MicrowaveBus -> Fan.MicrowaveBus;
			MicrowaveBusCon10: bus access MicrowaveBus -> GrillHeaterProcessor.MicrowaveBus;
			MicrowaveBusCon11: bus access MicrowaveBus -> MagnetronProcessor.MicrowaveBus;
			GrillHeaterBusCon1: bus access GrillHeaterBus -> GrillHeaterProcessor.GrillHeaterBus;
			GrillHeaterBusCon2: bus access GrillHeaterBus -> GrillHeater.GrillHeaterBus;
			GrillHeaterBusCon3: bus access GrillHeaterBus -> MicrowaveProcessor.GrillHeaterBus;
			MagnetronBusCon1: bus access MagnetronBus -> MagnetronProcessor.MagnetronBus;
			MagnetronBusCon2: bus access MagnetronBus -> Magnetron.MagnetronBus;
			MagnetronBusCon3: bus access MagnetronBus -> MicrowaveProcessor.MagnetronBus;
			-- ports
			TemperatureToMicrowaveProcessor: port TemperatureSensor.TempOut -> MicrowaveProcessorProcess.Temperature;
			DoorSensorToMicrowaveProcessor: port DoorSensor.DoorOpen -> MicrowaveProcessorProcess.DoorOpen;
			TouchScreenToTouchScreenProcess: port TouchScreen.Input -> TouchScreenProcess.TouchScreenInput;
			TouchScreenProcessToMicrowaveModeProcess: port TouchScreenProcess.MicrowaveMode -> MicrowaveProcessorProcess.MicrowaveMode;
			MicrowaveProcessorToFan: port MicrowaveProcessorProcess.FanPower -> Fan.FanPower;
			MicrowaveProcessorToLight: port MicrowaveProcessorProcess.LightOnOff -> Light.LightOnOff;
			MicrowaveProcessorToAlarm: port MicrowaveProcessorProcess.AlarmOnOff -> Alarm.AlarmOnOff;
			MicrowaveProcessorToTurntable: port MicrowaveProcessorProcess.TurntableOnOff -> Turntable.TurntableOnOff;
			MicrowaveProcessorToDisplay: port MicrowaveProcessorProcess.DisplayOutput -> Display.DisplayInput;
			MicrowaveProcessorToMagnetron: port MicrowaveProcessorProcess.MagnetronPowerLevel -> MagnetronProcessorProcess.PowerLevel;
			MicrowaveProcessorToGrillHeater: port MicrowaveProcessorProcess.GrillTemperature -> GrillHeaterProcessorProcess.TargetTemperature;
			GrillHeaterProcessorToGrillHeater: port GrillHeaterProcessorProcess.HeaterOnOff -> GrillHeater.HeatingOnOff;
			MagnetronProcessToMagnetron: port MagnetronProcessorProcess.MagnetronOnOff -> Magnetron.MagnetronOnOff;
		properties
			Actual_Connection_Binding => (reference(MicrowaveBus)) applies to TemperatureToMicrowaveProcessor;
			Actual_Connection_Binding => (reference(MicrowaveBus)) applies to DoorSensorToMicrowaveProcessor;
			Actual_Connection_Binding => (reference(MicrowaveBus)) applies to TouchScreenToTouchScreenProcess;
			Actual_Connection_Binding => (reference(MicrowaveBus)) applies to TouchScreenProcessToMicrowaveModeProcess;
			Actual_Connection_Binding => (reference(MagnetronBus)) applies to MicrowaveProcessorToMagnetron;
			Actual_Connection_Binding => (reference(GrillHeaterBus)) applies to MicrowaveProcessorToGrillHeater;
			Actual_Processor_Binding => (reference(MicrowaveProcessor)) applies to MicrowaveProcessorProcess;
			Actual_Processor_Binding => (reference(MicrowaveProcessor)) applies to TouchScreenProcess;
			Actual_Processor_Binding => (reference(MagnetronProcessor)) applies to MagnetronProcessorProcess;
			Actual_Processor_Binding => (reference(GrillHeaterProcessor)) applies to GrillHeaterProcessorProcess;
			
	end MicrowaveSystem.impl;
	
end microwave;